package ui;

import flixel.FlxSprite;
import flixel.group.FlxSpriteGroup;

using StringTools;

class Alphabet extends FlxSpriteGroup {
	public var text:String = '';
	public var isBold:Bool = false;

	private var letterOffset:Float = 0;

	public function new(x:Float, y:Float, str:String = "", ?bold:Bool = true) {
		super(x, y);

		isBold = bold;
		setText(str);
	}

	public function setText(value:String) {
		clear();

		text = value;
		letterOffset = 0;
		addText();
	}

	private function addText()
		for(character in text.split('')) {
			if (' -_'.contains(character) || !AlphaCharacter.completeList.contains(character)) {
				letterOffset += 40;
				continue;
			}

			var letter:AlphaCharacter = new AlphaCharacter(letterOffset, 0, character, isBold);
			letterOffset += letter.width;

			add(letter);
		}
}

class AlphaCharacter extends FlxSprite {
	public static inline var numbers:String		 = "1234567890";
	public static inline var symbols:String		 = "|~#$%()*+-<=>@[]^_.,'!?";
	public static inline var completeList:String = "abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ1234567890|~#$%()*+-<=>@[]^.,'!?";

	private var letter:String;
	private var replacementArray:Array<Array<Dynamic>> = [
		['.', "'", '?', '!'],
		['period', 'apostraphie', 'question mark', 'exclamation point'],
		[50, -5, 0, 0]
	];

	public function new(x:Float, y:Float, char:String, bolded:Bool) {
		super(x, y);

		var tex = Paths.sparrow('ui/alphabet');
		antialiasing = Settings.antialiasing;
		frames = tex;
		letter = char;

		bolded ? createBold() : createLetter();
	}

	public function createBold() {
		animation.addByPrefix(letter, letter.toUpperCase() + " bold", 12);
		animation.play(letter);
		updateHitbox();
	}

	public function createLetter() {
		var lower = letter.toLowerCase();
		var suffix = '';

		if (!numbers.contains(letter))
			suffix = lower == letter ? ' lowercase' : ' capital';

		if (symbols.contains(letter)) {
			replaceWithSymbol();
			suffix = '';
		}

		animation.addByPrefix(letter, '$lower$suffix', 12);
		animation.play(letter);
		updateHitbox();

		y = (110 - height);
	}

	public function replaceWithSymbol() 
		for(i in 0...replacementArray[0].length)
			if (letter == replacementArray[0][i]){
				letter = replacementArray[1][i];
				y     += replacementArray[2][i];
				break;
			}
}
